plugins {
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java'
    id "com.github.spotbugs" version "5.2.1"
    id "org.openapi.generator" version "7.0.1"
}

group = 'org.railwaystations'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = "21"
    targetCompatibility = "21"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

bootJar {
    mainClass.set('org.railwaystations.rsapi.app.RsapiApplication')
}

openApiValidate {
    inputSpec = "$rootDir/src/main/resources/static/openapi.yaml".toString()
    recommend = true
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/static/openapi.yaml".toString()
    outputDir = "$buildDir/openapi".toString()
    apiPackage = "org.railwaystations.rsapi.adapter.in.web.api"
    modelPackage = "org.railwaystations.rsapi.adapter.in.web.model"
    modelNameSuffix = "Dto"
    cleanupOutput = true
    configOptions = [
            interfaceOnly        : "true",
            useSpringController  : "true",
            openApiNullable      : "false",
            skipDefaultInterface : "true",
            dateLibrary          : "java8",
            documentationProvider: "none",
            annotationLibrary    : "none",
            useTags              : "true",
            useBeanValidation    : "true",
            useSpringBoot3       : "true",
            implicitHeadersRegex : "(Authorization|Upload-Token|Email)"
    ]
}

sourceSets.main.java.srcDirs += 'build/openapi/src/main/java'

compileJava.dependsOn tasks.openApiGenerate

ext {
    testContainersVersion = '1.19.1'
    jdbiVersion = '3.41.3'
    swaggerRequestValidatorVersion = '2.38.0'
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.security:spring-security-oauth2-authorization-server:1.1.3"
    implementation 'org.liquibase:liquibase-core'
    implementation "org.jdbi:jdbi3-spring5:$jdbiVersion"
    implementation "org.jdbi:jdbi3-sqlobject:$jdbiVersion"
    implementation "com.goterl:lazysodium-java:5.1.4"
    implementation "net.java.dev.jna:jna:5.13.0"
    implementation "commons-codec:commons-codec"
    implementation "commons-io:commons-io:2.15.0"
    implementation "org.apache.commons:commons-lang3"
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.17'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.webjars:bootstrap:5.3.2'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    runtimeOnly 'org.webjars:webjars-locator-core'

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.awaitility:awaitility"
    testImplementation "org.testcontainers:testcontainers:$testContainersVersion"
    testImplementation "org.testcontainers:junit-jupiter:$testContainersVersion"
    testImplementation "org.testcontainers:mariadb:$testContainersVersion"
    testImplementation "com.atlassian.oai:swagger-request-validator-core:$swaggerRequestValidatorVersion"
    testImplementation "com.atlassian.oai:swagger-request-validator-spring-webmvc:$swaggerRequestValidatorVersion"
    testImplementation "com.atlassian.oai:swagger-request-validator-mockmvc:$swaggerRequestValidatorVersion"
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.xmlunit:xmlunit-assertj3'
    testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone:3.0.1'
    testImplementation 'org.mockito:mockito-core'
}

test {
    useJUnitPlatform()
}

spotbugs {
    ignoreFailures.set(true)
}

spotbugsTest {
    ignoreFailures = true
}
